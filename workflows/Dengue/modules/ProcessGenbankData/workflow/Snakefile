"""Process data from Genbank

Params:
    Start date (str): Start date for the data to be processed (e.g. "2000-01-01")
    End date (str): End date for the data to be processed (e.g. "2023-12-24")
    Host (str): Host for the data to be processed (e.g. "Homo sapiens")
"""
configfile: "config/config.yaml"

indir = config["input_namespace"]
outdir = config["output_namespace"]
params = config["params"]

def script(name=""):
    from snakemake.remote import AUTO

    filename = srcdir(f"scripts/{name}")
    try:
        return AUTO.remote(filename)
    except TypeError:
        return filename

rule process_genbank_data:
    input:
        metadata=f"results/{indir}/metadata.tsv",
        fasta=f"results/{indir}/ncbi_dataset/data/genomic.fna",
        script=script("clean_metadata_and_fasta_general.R"),
    output:
        fasta_files=f"results/{outdir}/Unaligned.fasta",
        info_tables_txt=f"results/{outdir}/infoTbl.txt",
        info_tables_csv=f"results/{outdir}/infoTbl.csv",
    params:
        outdir=f"results/{outdir}",
        start_date=params["Start date"],
        end_date=params["End date"],
        host=params["Host"],
    log:
        "logs/process_genomic_data.log",
    message:
        "Processing and cleaning Dengue data downloaded from NCBI"
    conda:
        "envs/genbank.yaml"
    shell:
        """
        mkdir -p {params.outdir}
        rscript {input.script} \
            --metadata {input.metadata} \
            --fasta {input.fasta} \
            --start-date {params.start_date} \
            --end-date {params.end_date} \
            --host "{params.host}" \
            --outfile_fasta {output.fasta_files} \
            --outfile_csv {output.info_tables_csv} \
            --outfile_tsv {output.info_tables_txt} \
            > {log} 2>&1
        """

rule _test:
    input:
        f"results/{outdir}/Unaligned.fasta",
        f"results/{outdir}/infoTbl.txt",
        f"results/{outdir}/infoTbl.csv",
