"""Inferring time-calibrated trees for each Dengue virus serotype

Params:
    Clock filter IQD (number): Interquartile range for clock filter
"""
configfile: "config/config.yaml"

indir = config["input_namespace"]
outdir = config["output_namespace"]
params = config["params"]


rule treetime:
    input:
        aln=f"results/{indir['metadata']}/subsampled_{{serotype}}.fasta",
        ml_tree=f"results/{indir['fasta']}/subsampled_{{serotype}}.fasta.treefile",
        metadata=f"results/{indir['metadata']}/subsampled_{{serotype}}_infoTbl.csv",
    output:
        tree=f"results/{outdir}/timetree_{{serotype}}.tree",
        branch_lengths=f"results/{outdir}/{{serotype}}_branch_lengths.json",
    params:
        clock_filter=params['Clock filter IQD'],
    conda:
        "envs/nextstrain_all.yaml"
    log:
        "logs/treetime_{serotype}.log",
    message:
        "Inferring time-calibrated trees for each Dengue virus serotype."
    shell:
        """
        augur refine \
            --tree {input.ml_tree} \
            --alignment {input.aln} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.branch_lengths} \
            --timetree \
            --coalescent opt \
            --date-confidence \
            --clock-filter-iqd {params.clock_filter} \
            --root best > {log} 2>&1
        """

rule _test:
    input:
        f"results/{outdir}/timetree_Dengue_1.tree",
        f"results/{outdir}/timetree_Dengue_2.tree",
        f"results/{outdir}/timetree_Dengue_3.tree",
        f"results/{outdir}/timetree_Dengue_4.tree",
