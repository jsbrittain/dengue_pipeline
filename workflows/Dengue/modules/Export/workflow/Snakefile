configfile: "config/config.yaml"


indir = config["input_namespace"]
outdir = config["output_namespace"]

serotype = ["Dengue_1", "Dengue_2", "Dengue_3", "Dengue_4"]


def resource(name=""):
    from snakemake.remote import AUTO

    filename = srcdir(f"../resources/{name}")
    try:
        return AUTO.remote(filename)
    except TypeError:
        return filename


rule all:
    input:
        expand(
            "results/{outdir}/dengue_{serotype}.json",
            outdir=outdir,
            serotype=serotype,
        ),


"""Exporting data files for auspice"""


rule export:
    input:
        tree=f"results/{indir['treetime']}/timetree_{{serotype}}.tree",
        metadata=f"results/{indir['subsample']}/subsampled_{{serotype}}_infoTbl.csv",
        branch_lengths=f"results/{indir['treetime']}/{{serotype}}_branch_lengths.json",
        traits=f"results/{indir['mugrations']}/traits_{{serotype}}.json",
        aa_muts=f"results/{indir['translation']}/aa_muts_{{serotype}}.json",
        nt_muts=f"results/{indir['mutations']}/subsampled_{{serotype}}_mutations.json",
        auspice_config=resource("auspice_config.json"),
    output:
        auspice_json=f"results/{outdir}/dengue_{{serotype}}.json",
    conda:
        "envs/nextstrain_all.yaml"
    log:
        "logs/export_{serotype}.log",
    message:
        "Export for visualisation in Auspice"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.branch_lengths} {input.traits} {input.nt_muts} {input.aa_muts} \
            --auspice-config {input.auspice_config} \
            --output {output.auspice_json} > {log} 2>&1
        """


rule _test:
    input:
        f"results/{outdir}/dengue_Dengue_1.json",
        f"results/{outdir}/dengue_Dengue_2.json",
        f"results/{outdir}/dengue_Dengue_3.json",
        f"results/{outdir}/dengue_Dengue_4.json",
